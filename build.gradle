apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

/*
 * Gets the version name from the latest Git tag
 */
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = getVersionName()
group = 'de.jodamob.android'


configurations {
    provided
}

repositories {
    mavenCentral()
	maven {
        url "http://download.crashlytics.com/maven"
    }
}

dependencies {
	testCompile 'junit:junit:4.11'
	testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.robolectric:robolectric:2.2'
    testCompile 'org.robolectric:android-all:4.4_r1-robolectric-0'
    provided 'org.robolectric:android-all:4.4_r1-robolectric-0'
    provided 'com.newrelic.agent.android:android-agent:3.239.0'
	provided 'com.crashlytics.android:crashlytics:1.1.5'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

sourceSets {
    main { compileClasspath += configurations.provided }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

compileJava {
  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'jodamoblog'
                packaging 'jar'
                description 'Just another android logging framework'
                url 'https://github.com/dpreussler/jodamoblog'

                scm {
                    url 'scm:git@github.com:dpreussler/jodamoblog.git'
                    connection 'scm:git@github.com:dpreussler/jodamoblog.git'
                    developerConnection 'scm:git@github.com:dpreussler/jodamoblog.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/dpreussler/jodamoblog/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'dpreussler'
                        name 'Danny Preussler'
                    }
                }
            }
        }
    }
}